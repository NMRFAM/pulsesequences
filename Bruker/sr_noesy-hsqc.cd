;sr-noesyhsqcf3gpsi3d
;avance-version (12/01/11)
;NOESY-HSQC
;3D sequence with
;   super-resolution 1H NOESY dimension
;   homonuclear correlation via dipolar coupling
;   dipolar coupling may be due to noe or chemical exchange.
;   H-1/X correlation via double inept transfer
;      using sensitivity improvement
;phase sensitive (t1)
;phase sensitive using Echo/Antiecho-TPPI gradient selection (t2)
;with decoupling during acquisition
;using trim pulses in inept transfer
;using f3 - channel
;(use parameterset NOESYHSQCF3GPSI3D)
;
;Lorieau JL. J. Biomol. NMR. 73, 477-491 (1991)
;A.G. Palmer III, J. Cavanagh, P.E. Wright & M. Rance, J. Magn.
;   Reson. 93, 151-170 (1991)
;L.E. Kay, P. Keifer & T. Saarinen, J. Am. Chem. Soc. 114,
;   10663-5 (1992)
;J. Schleucher, M. Schwendinger, M. Sattler, P. Schmidt, O. Schedletzky,
;   S.J. Glaser, O.W. Sorensen & C. Griesinger, J. Biomol. NMR 4,
;   301-306 (1994)
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d11=30m"
"d12=20u"
"d13=4u"
"d26=1s/(cnst4*4)"


"d0=3u"
"d10=3u"
"d20=d0*2*cnst20"

"in0=inf1/2"
"in10=inf2/2"
"in20=inf1*cnst20"


"DELTA1=d13+p16+d16+4u"

#   ifdef LABEL_CN
"DELTA=p16+d16+larger(p2,p14)+d10*2"
"DELTA2=larger(p14,p22)+d0*2"
#   else
"DELTA=p16+d16+p2+d10*2"
"DELTA2=p22+d0*2"
#   endif /*LABEL_CN*/

"TAU=d8-p16-d16"


aqseq 321


1 ze
  d11 pl16:f3
2 d11 do:f3
3 d12
  d1 
  (p1 ph8)
  DELTA2 pl3:f3
  (p2 ph9)
  d0

#   ifdef LABEL_CN
  (center (p14:sp3 ph1):f2 (p22 ph1):f3 )
#   else
  (p22 ph1):f3
#   endif /*LABEL_CN*/

  d0
  (p1 ph10)
  TAU UNBLKGRAD
  d20
  p16:gp1
  d16
  (p1 ph11)
  d26
  (center (p2 ph1) (p22 ph6):f3 )
  d26
  p28 ph1
  d13
  (p1 ph2) 
  3u
  p16:gp2
  d16
  (p21 ph3):f3
  d10 

#   ifdef LABEL_CN
  (center (p2 ph7) (p14:sp3 ph1):f2 )
#   else
  (p2 ph7)
#   endif /*LABEL_CN*/

  d10
  p16:gp3*EA
  d16
  (p22 ph4):f3
  DELTA
  (center (p1 ph1) (p21 ph4):f3 )
  d24
  (center (p2 ph1) (p22 ph1):f3 )
  d24
  (center (p1 ph2) (p21 ph5):f3 )
  d26
  (center (p2 ph1) (p22 ph1):f3 )
  d26
  (p1 ph1)
  DELTA1
  (p2 ph1)
  d13
  p16:gp4
  d16 pl16:f3
  4u BLKGRAD
  go=2 ph31 cpd3:f3
  d11 do:f3 mc #0 to 2 
     F1PH(calph(ph8, +90) & calph(ph9, +90), caldel(d0, +in0), caldel(d20, +in20)) 
     F2EA(calgrad(EA) & calph(ph5, +180), caldel(d10, +in10) & calph(ph3, +180) & calph(ph6, +180) & calph(ph31, +180))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=0 0 2 2
ph5=1 1 3 3
ph6=0
ph7=0 0 2 2
ph8=0 0 0 0 2 2 2 2
ph9=1 1 1 1 3 3 3 3
ph10=0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
     2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
ph11=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph31=0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0
     2 0 0 2 0 2 2 0 0 2 2 0 2 0 0 2


;pl1 : f1 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl16: f3 channel - power level for CPD/BB decoupling
;sp3: f2 channel - shaped pulse 180 degree (adiabatic)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p14: f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;p16: homospoil/gradient pulse                         [1 msec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p28: f1 channel - trim pulse                          [1 msec]
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d8 : = a0 (srNOESY base mixing time)
;d10: incremented delay (F2 in 3D)                     [3 usec]
;d11: delay for disk I/O                               [30 msec]
;d12: delay for power switching                        [20 usec]
;d13: short delay                                      [4 usec]
;d16: delay for homospoil/gradient recovery
;d24: 1/(4J)YH for YH
;     1/(8J)YH for all multiplicities
;d26: 1/(4J(YH))
;cnst4: = J(YH)
;cnst20: a1 (srNOESY linear t1 factor)
;inf1: 1/SW(H) = 2 * DW(H)
;inf2: 1/SW(X) = 2 * DW(X)
;in0: 1/(2 * SW(H)) = DW(H)
;nd0: 2
;in10: 1/(2 * SW(X)) = DW(X)
;nd10: 2
;ns: 8 * n
;ds: >= 16
;td1: number of experiments in F1
;td2: number of experiments in F2
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: echo-antiecho in F2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:    gp 1 : gp 2 : gp 3 : gp 4
;                         30 :   50 :   80 :  8.1      for N-15
;                         30 :   50 :   80 : 20.1      for C-13

;for z-only gradients:
;gpz1: 30%
;gpz2: 50%
;gpz3: 80%
;gpz4: 8.1% for N-15, 20.1% for C-13

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100


                                          ;preprocessor-flags-start
;LABEL_CN: for C-13 and N-15 labeled samples start experiment with 
;             option -DLABEL_CN (eda: ZGOPTNS)
                                          ;preprocessor-flags-end



;$Id: noesyhsqcf3gpsi3d,v 1.5.8.1 2012/01/31 17:56:34 ber Exp $
